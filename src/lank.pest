// * Zero or more
// + One or more


Program = _{ SOI ~ Form* ~ EOI }

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ SingleLineComment | MultilineComment }
SingleLineComment = _{ "//" ~ (!NEWLINE ~ ANY)* ~ (NEWLINE | EOI) }
MultilineComment = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

Form = { QuotedForm | NonQuotedForm }
QuotedForm = ${ "`" ~ NonQuotedForm }
NonQuotedForm = { "(" ~ (Primitive | Vec | Map | Form)+ ~ ")" }

Vec = { "[" ~ ((Form | Primitive | Vec) ~ WHITESPACE*)* ~ "]" }
Map = { "{" ~ ((Form | Primitive | Vec) ~ (Form | Primitive | Vec))* ~ "}"}

Primitive = { String | Char | Bool | Number | Symbol }

String = { "\"" ~ StringInner ~ "\"" }
StringInner = @{ (!("\"" | "\'") ~ ANY)* }
Char = { "\'" ~ ('a'..'z' | 'A'..'Z'){1} ~ "\'"}

Bool = @{ "true" | "false"}

Number = @{ "-"? ~ ASCII_DIGIT+ ~ "."? ~ ASCII_DIGIT*}

Symbol = @{ (!SymbolForbiddenChar ~ ANY)+ }
SymbolForbiddenChar = { "\"" | ";" | "(" | ")" | "[" | "]" | "`" | "'" | WHITESPACE }
