// * Zero or more
// + One or more
Program = _{ SOI ~ Form* ~ EOI }

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ SingleLineComment | MultilineComment }
SingleLineComment = _{ "//" ~ (!NEWLINE ~ ANY)* ~ (NEWLINE | EOI) }
MultilineComment = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

QuotedPrimitive = @{ "'" ~ (Number | Vec | Char | String | Bool | Symbol)}

None = @{ "None" }
Char = { "\'" ~ ('a'..'z' | 'A'..'Z'){1} ~ "\'"}
Number = @{ HexDigit | BinaryDigit | FloatLiteral}
    FloatLiteral = {"-"? ~ (ASCII_DIGIT)+ ~ "."? ~ ASCII_DIGIT*}
    BinaryDigit = {"0b" ~ ASCII_BIN_DIGIT{1, 16}}
    HexDigit = {"0x" ~ ASCII_HEX_DIGIT{2, 16}}
Primitive = { None | String | Char | Bool | Number | Symbol }

Form = { QuotedForm | NonQuotedForm }
QuotedForm = { "'" ~ NonQuotedForm }
NonQuotedForm = { "(" ~ (Primitive | Vec | Map | Form)* ~ ")" }

Vec = { "[" ~ ((Form | Primitive | Vec | Map) ~ WHITESPACE*)* ~ "]" }
Map = ${ "{" ~  WHITESPACE*  ~ ((Primitive | Form | Vec) ~ WHITESPACE+ ~ (Primitive | Form | Vec | Map) ~ WHITESPACE*)* ~ "}"}

String = { "\"" ~ StringInner ~ "\"" }
StringInner = @{ (!("\"" | "\'") ~ ANY)* }

Bool = @{ "true" | "false"}

Symbol = @{ (!SymbolForbiddenChar ~ ANY)+ }
SymbolForbiddenChar = { "\"" | ";" | "(" | ")" | "[" | "]" | "{" | "}" | "`" | "'" | WHITESPACE }
